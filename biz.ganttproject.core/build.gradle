// import com.sun.org.apache.xalan.internal.xsltc.compiler.Copy

buildscript {
    ext.kotlin_version = '1.6.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'maven-publish'

configurations {
    direct
}

dependencies {
    providedCompile project(path: ':biz.ganttproject.app.libs', configuration: 'exported')
    direct fileTree(dir: project.ext.libDir, include: ['*.jar'])
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.+'
    api 'org.jetbrains.kotlinx:kotlinx-coroutines-javafx:1.5.+'
    api 'org.jetbrains.kotlin:kotlin-reflect:1.6.0'
    api "com.google.guava:guava:31.+"

    direct configurations.api
    implementation configurations.direct
    implementation configurations.providedCompile

    testImplementation(
        'org.junit.jupiter:junit-jupiter-api:5.+',
        'com.nhaarman.mockitokotlin2:mockito-kotlin:2.+'
    )
    testRuntimeOnly(
        'org.junit.jupiter:junit-jupiter-engine:5.+'
    )
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
    }
}

test {
    useJUnitPlatform()
}

task copyPlugin(dependsOn: assemble) {
    doFirst {
        println "Copying $project.name to $rootProject.pluginsDir"
    }
    doLast {
        println "Installing $project.name"
        copy {
            into(new File(rootProject.pluginsDir, project.name))
            from(fileTree(".")) {
                include "plugin.xml"
            }
            from(fileTree("src/main/")) {
                include "resources/**"
            }
        }
        copy {
            into(new File(rootProject.pluginsDir, "${project.name}/lib/"))
            from(configurations.direct) {
                include "*.jar"
            }
            from(jar.outputs.getFiles().getFiles().flatten())
            rename { filename -> filename + ".lib" }
        }
        println "<<< $project.name"
    }

}

addPublishing(project)
publishing {
    publications {
        core(MavenPublication) {
            artifactId = 'biz.ganttproject.core'
            artifact jar
        }
    }
}
